% Bower - a frontend for the Notmuch email system
% Copyright (C) 2013 Peter Wang

:- module send_util.
:- interface.

:- import_module io.

:- pred get_from(string::out, io::di, io::uo) is det.

:- pred generate_date_msg_id(string::out, string::out, io::di, io::uo) is det.

:- pred write_unstructured_header(io.output_stream::in, string::in, string::in,
    io::di, io::uo) is det.

:- pred write_address_list_header(io.output_stream::in, string::in, string::in,
    io::di, io::uo) is det.

:- pred write_references_header(io.output_stream::in, string::in, string::in,
    io::di, io::uo) is det.

:- pred generate_boundary(string::out, io::di, io::uo) is det.

%-----------------------------------------------------------------------------%
%-----------------------------------------------------------------------------%

:- implementation.

:- import_module char.
:- import_module int.
:- import_module list.
:- import_module random.
:- import_module require.
:- import_module string.
:- import_module time.

:- import_module callout.
:- import_module fold_lines.
:- import_module sys_util.
:- import_module time_util.

:- mutable(msgid_counter, int, 0, ground, [untrailed, attach_to_io_state]).

%-----------------------------------------------------------------------------%

get_from(From, !IO) :-
    get_notmuch_config("user.name", ResName, !IO),
    (
        ResName = ok(Name),
        get_notmuch_config("user.primary_email", ResEmail, !IO),
        (
            ResEmail = ok(Email),
            From = string.append_list([Name, " <", Email, ">"])
        ;
            ResEmail = error(_),
            From = Name
        )
    ;
        ResName = error(_),
        From = ""
    ).

%-----------------------------------------------------------------------------%

generate_date_msg_id(Date, MessageId, !IO) :-
    time(Time, !IO),
    TM = localtime(Time),
    Year = 1900 + TM ^ tm_year,
    Month = 1 + TM ^ tm_mon,
    Day = TM ^ tm_mday,
    Hour = TM ^ tm_hour,
    Min = TM ^ tm_min,
    Sec = TM ^ tm_sec,
    Wday = TM ^ tm_wday,
    (
        weekday_short_name(Wday, WdayName0),
        month_short_name(Month, MonthName0)
    ->
        WdayName = WdayName0,
        MonthName = MonthName0
    ;
        unexpected($module, $pred, "bad weekday or month")
    ),
    get_timezone(Time, Tz0),
    Tz = Tz0 // 60,
    TzHour = Tz // 60,
    TzMin = abs(Tz) mod 60,
    Date = string.format("%s, %d %s %d %02d:%02d:%02d %+03d%02d",
        [s(WdayName), i(Day), s(MonthName), i(Year), i(Hour), i(Min), i(Sec),
        i(TzHour), i(TzMin)]),

    % This emulates the Message-ID generated by Mutt.

    get_msgid_counter(Counter0, !IO),
    char.det_from_int(65 + Counter0, Char),
    Counter = (Counter0 + 1) mod 26,
    set_msgid_counter(Counter, !IO),

    get_pid(Pid, !IO),
    get_hostname(HostName, !IO),
    get_domainname(DomainName, !IO),
    MessageId = string.format("<%04d%02d%02d%02d%02d%02d.G%c%d@%s.%s>",
        [i(Year), i(Month), i(Day), i(Hour), i(Min), i(Sec),
        c(Char), i(Pid), s(HostName), s(DomainName)]).

%-----------------------------------------------------------------------------%

% XXX quote non-ASCII Values; or can we get away with UTF-8 now?

write_unstructured_header(Stream, Field, Value, !IO) :-
    get_spans_by_whitespace(Value, ValueSpans),
    add_field_span(Field, ValueSpans, Spans),
    fill_lines(soft_line_length, Spans, Lines),
    do_write_header(Stream, Lines, !IO).

write_address_list_header(Stream, Field, Value, !IO) :-
    get_spans_by_comma(Value, ValueSpans),
    add_field_span(Field, ValueSpans, Spans),
    fill_lines(soft_line_length, Spans, Lines),
    do_write_header(Stream, Lines, !IO).

write_references_header(Stream, Field, Value, !IO) :-
    write_unstructured_header(Stream, Field, Value, !IO).

:- pred add_field_span(string::in, list(span)::in, list(span)::out) is det.

add_field_span(Field, Spans0, Spans) :-
    FieldColon = Field ++ ":",
    (
        Spans0 = [],
        Spans = [span(FieldColon, "")]
    ;
        Spans0 = [Head0 | Tail],
        Head0 = span(Mandatory, Trailer),
        Head = span(FieldColon ++ " " ++ lstrip(Mandatory), Trailer),
        Spans = [Head | Tail]
    ).

:- pred do_write_header(io.output_stream::in, list(string)::in,
    io::di, io::uo) is det.

do_write_header(Stream, Lines, !IO) :-
    io.write_list(Stream, Lines, "\n ", io.write_string, !IO),
    io.nl(Stream, !IO).

:- func soft_line_length = int.

soft_line_length = 78.

%-----------------------------------------------------------------------------%

generate_boundary(Boundary, !IO) :-
    % This emulates the boundaries generated by Mutt.
    time(Time, !IO),
    time_to_int(Time, Seed),
    random.init(Seed, RS),
    Len = 16,
    list.map_foldl(generate_boundary_char, 1 .. Len, Chars, RS, _RS),
    string.from_char_list(Chars, Boundary).

:- pred generate_boundary_char(int::in, char::out,
    random.supply::mdi, random.supply::muo) is det.

generate_boundary_char(_, Char, !RS) :-
    random.random(0, 64, Index, !RS),
    string.unsafe_index(base64_chars, Index, Char).

:- func base64_chars = string.

base64_chars =
    "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/".

%-----------------------------------------------------------------------------%
% vim: ft=mercury ts=4 sts=4 sw=4 et
